<?xml version="1.0" encoding="shift_jis" ?>
<xsl:stylesheet
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:xhtml="http://www.w3.org/1999/xhtml"
  exclude-result-prefixes="xhtml"
  version="1.0">
<xsl:output method="xml" encoding="utf-8" omit-xml-declaration="yes" indent="yes"/>
<xsl:strip-space elements="*" />

<!-- 全体 -->
<xsl:template match="/">
  <記事>
    <xsl:attribute name="タイトル">

<xsl:choose>
<xsl:when test="/xhtml:html/xhtml:body/xhtml:h1">
<xsl:value-of select="/xhtml:html/xhtml:body/xhtml:h1"/>
</xsl:when>
<xsl:otherwise>
<xsl:value-of select="/xhtml:html/xhtml:head/xhtml:title"/>
</xsl:otherwise>
</xsl:choose>
    </xsl:attribute>
    <xsl:attribute name="備考">
      <xsl:text>trasnDataType : commonXHTML</xsl:text>
    </xsl:attribute>
<xsl:apply-templates/>
  </記事>
</xsl:template>

<xsl:template match="xhtml:html">
<xsl:apply-templates/>
</xsl:template>

<!-- ヘッダ -->
<xsl:template match="xhtml:head">
<ヘッダ>
<xsl:comment>
<xsl:text>This document was generated by Edamame and xhtml2xml.xsl</xsl:text>
</xsl:comment>
<行 />
<xsl:text>タイトル：</xsl:text> <xsl:value-of select="../xhtml:body/xhtml:h1"/>
<行 />
</ヘッダ>
</xsl:template>

<xsl:template match="xhtml:body">
<テキスト 種別="本文">
<xsl:apply-templates/>
</テキスト>
</xsl:template>

<!-- タイトル部分 -->
<xsl:template match="xhtml:h1|xhtml:h2|xhtml:h3">
<タイトル>
<xsl:attribute name="種別"><xsl:value-of select="name()"/></xsl:attribute>
<xsl:apply-templates/>
</タイトル><xsl:text>&#x0A;</xsl:text>
</xsl:template>

<xsl:template match="xhtml:p">
  <xsl:apply-templates/><b /><xsl:text>&#x0A;</xsl:text>
</xsl:template>

<!-- 傍点 -->
<xsl:template match="xhtml:strong">
  <span 種別="傍点">
    <xsl:attribute name="備考">
      <xsl:value-of select="@class"/>
    </xsl:attribute>
    <xsl:apply-templates/>
  </span>
</xsl:template>

<!-- 傍線 -->
<xsl:template match="xhtml:em">
  <span 種別="傍線">
    <xsl:attribute name="備考">
      <xsl:value-of select="@class"/>
    </xsl:attribute>
    <xsl:attribute name="xhtml:style">
      <xsl:value-of select="@style"/>
    </xsl:attribute>
    <xsl:apply-templates/>
  </span>
</xsl:template>

<!-- 太字 -->
<xsl:template match="xhtml:b">
  <span 種別="太字"><xsl:apply-templates/></span>
</xsl:template>

<!-- 小書 -->
<xsl:template match="xhtml:small">
  <小書><xsl:apply-templates/></小書>
</xsl:template>

<!-- 上付き・下付き -->
<xsl:template match="xhtml:sup">
  <span 種別="強調" 備考="上付き"><xsl:apply-templates/></span>
</xsl:template>

<xsl:template match="xhtml:sub">
  <span 種別="強調" 備考="下付き"><xsl:apply-templates/></span>
</xsl:template>

<!-- 改行 -->
<xsl:template match="xhtml:br">
  <行 />
</xsl:template>

<!-- ルビ -->
<xsl:template match="xhtml:ruby">
   <xsl:apply-templates/>
</xsl:template>

<xsl:template match="xhtml:rb">
  <r>
    <xsl:attribute name="rt">
      <xsl:value-of select="../rt"/>
    </xsl:attribute>
    <xsl:apply-templates/>
  </r>
</xsl:template>

<xsl:template match="xhtml:rt">
</xsl:template>

<xsl:template match="xhtml:rp">
</xsl:template>

<xsl:template match="xhtml:ruby" mode="insideAttribute">
   <xsl:value-of select="text()"/>
</xsl:template>

<!-- 画像 -->
<xsl:template match="xhtml:img">
  <画像>

   <xsl:attribute name="alt">
     <xsl:value-of select="@alt"/>
   </xsl:attribute>

   <xsl:attribute name="height">
     <xsl:value-of select="@height"/>
   </xsl:attribute>

   <xsl:attribute name="width">
     <xsl:value-of select="@width"/>
   </xsl:attribute>

   <xsl:attribute name="備考">
     <xsl:value-of select="@class"/>
   </xsl:attribute>

  </画像>

</xsl:template>


<!-- 水平線 -->
<xsl:template match="xhtml:hr">
  <hr />
</xsl:template>


<!-- リスト・テーブル -->
<xsl:template match="xhtml:li">
  <xsl:apply-templates/><行 />
</xsl:template>

<xsl:template match="xhtml:tr">
  <xsl:apply-templates/><行 />
</xsl:template>

<xsl:template match="xhtml:td">
  <xsl:apply-templates/><xsl:text>　</xsl:text>
</xsl:template>


</xsl:stylesheet>
